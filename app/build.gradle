apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix("." + GradleUtil.getBuildIdentifier(project))
        }
        release {
            if (GradleUtil.isUsabilityBuild(project)) {
                applicationIdSuffix("." + GradleUtil.getBuildIdentifier(project))
            }

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        javaCrack {
            applicationId "com.khalid.javaCrack"

            def major = 1
            def minor = 0
            def patch = 0
            def build = 0
            versionCode GradleUtil.versionCode(major, minor, patch, build)
            versionName GradleUtil.versionName(major, minor, patch, build)

        }
        androidCrack {
            applicationId "com.khalid.androidCrack"

            def major = 1
            def minor = 0
            def patch = 0
            def build = 0
            versionCode GradleUtil.versionCode(major, minor, patch, build)
            versionName GradleUtil.versionName(major, minor, patch, build)

        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'

        debug {
            assets.srcDirs += '../lib/mocked/templates'
        }

    }
}
kapt {
    generateStubs = true
}

dependencies {

    //COMPILE
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':lib:mocked:mock3')
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:cardview-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:design:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:cardview-v7:${rootProject.ext.supportLibVersion}"
    compile "com.jakewharton:butterknife:${butterknife_version}"

    //Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:${kotlin_version}"
    provided 'javax.annotation:jsr250-api:1.0'

    //RX
    compile 'io.reactivex:rxandroid:1.2.1'
    compile "io.reactivex:rxjava:${rxjava_version}"
    compile 'com.jakewharton.rxbinding:rxbinding:1.0.0'

    // Dagger 2
    compile "com.google.dagger:dagger:${dagger_version}"
    kapt "com.google.dagger:dagger-compiler:${dagger_version}"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:${retrofit2_version}"
    compile "com.squareup.retrofit2:converter-moshi:${retrofit2_version}"
    compile "com.squareup.okhttp3:logging-interceptor:${okhttp_version}"

    //Timber
    compile 'com.jakewharton.timber:timber:4.5.1'

    //DEBUG COMPILE
    debugCompile "com.squareup.okhttp3:mockwebserver:${okhttp_version}"

    def stethoVersion = '1.5.0'
    debugCompile "com.facebook.stetho:stetho:${stethoVersion}"
    debugCompile "com.facebook.stetho:stetho-okhttp3:${stethoVersion}"

    //TEST COMPILE
    testCompile 'junit:junit:4.12'

    def roboVersion = '3.0'
    testCompile "org.robolectric:robolectric:${roboVersion}"
    testCompile "org.robolectric:shadows-multidex:${roboVersion}"
    testCompile 'org.mockito:mockito-core:2.0.31-beta'

    def espressoVersion = '2.2.1'
    androidTestCompile("com.android.support.test.espresso:espresso-core:${espressoVersion}", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:${espressoVersion}") {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
    }


}
repositories {
    mavenCentral()
    maven {
        url "http://repository.jetbrains.com/all"
    }
}

public class GradleUtil {

    static def isDefined(s) {
        return s != null && !s.isEmpty() && s != "null"
    }

    static def getBuildIdentifier(Project project) {
        return getPropertyWithDefault(project, "id", "debug")
    }

    static def getPropertyWithDefault(Project project, String key, String thedefault) {
        if (project.hasProperty(key)) {
            return project.getProperty(key).toString()
        }

        return thedefault
    }

    static def isUsabilityBuild(Project project) {
        return getBuildIdentifier(project) == "usability"
    }

    static def versionCode(major, minor, patch, build) {
        return major * 1000000 + minor * 10000 + patch * 100 + build
    }

    static def versionName(major, minor, patch, build) {
        def name = "${major}.${minor}.${patch}"
        if (build > 0) {
            name = "${name}.${build}"
        }
        return name
    }

    static def getAppName(Project project, variant) {
        // Don't touch release builds
        if (variant.buildType.name == "release" || isUsabilityBuild(project)) {
            return null
        }

        def flavor = variant.productFlavors.get(0).name
        flavor = flavor.capitalize()

        def type = variant.buildType.name
        if (type == "debug") {
            type = getBuildIdentifier(project)
        }
        type = type.capitalize()

        def appname = flavor + " " + type
        return appname.tr('_', '.')
    }
}


android.applicationVariants.all { variant ->

    def launcherName = GradleUtil.getAppName(project, variant)
    if (!GradleUtil.isDefined(launcherName)) {
        // Must be a release build, so we default to app_name
        launcherName = "@string/app_name"
    }
    variant.resValue "string", "launcher_name", "${launcherName}"

}